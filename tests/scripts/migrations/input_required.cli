#!/usr/bin/env clitest --v0

include "../_include/common.cli.inc";

ignore {
    sequence {
        ! Automatic backup is enabled, see the full backup list with %{GREEDYDATA}
        ! Read more at https://geldata.com/p/localdev
        ! Running automatic backup of local instance %{DATA}...
        ! Created full backup:
        ! %{DATA} MiB %{PATH}
    }
}

set BRANCH "input_required_test";
set INSTANCE "__shared-test-db__";

$ gel instance create -I $INSTANCE
%EXIT any
*

# Log the instance logs in the background to help debug failures
background {
    $ gel instance logs --tail 10 --follow -I $INSTANCE
    %EXIT any
    *
}

$ gel branch drop $BRANCH --force --non-interactive
%EXIT any
*

using tempdir;

using new dir "input_required_test";

# Initialize a project
$ echo "[instance]" > gel.toml
$ cp -R $INITIAL_PWD/../../migrations/db3 dbschema
*

$ gel project init --non-interactive --link --server-instance $INSTANCE --branch $BRANCH
! Found `gel.toml` in %{PATH}
! Linking project...
! Applying migrations...
*
! Writing gel.local.toml for configuration
! Project linked
! To connect to __shared-test-db__, run `gel`

# Destroy the branch after the test
defer {
    $ gel project unlink
    *
    $ gel branch drop -I $INSTANCE $BRANCH --force --non-interactive
    %EXIT any
    *
}

$ gel project info
*

$ gel migration create --non-interactive
%EXIT 1
! Input required: Please specify a conversion expression to alter the type of link 'foo'
! gel error: Cannot apply migration without user input

$ printf "yes\n.foo[IS default::Child2]\n" | gel migration create --expert
! did you alter the type of link 'foo' of object type 'default::Base'? [y,n,l,c,b,s,q,?]
! Please specify a conversion expression to alter the type of link 'foo':
! cast_expr> 
! Created %{DATA}, id: %{DATA}

$ cat dbschema/migrations/00002-*.edgeql
! CREATE MIGRATION %{DATA}
!     ONTO m1d6kfhjnqmrw4lleqvx6fibf5hpmndpw2tn2f6o4wm6fjyf55dhcq
! {
!   ALTER TYPE default::Base {
!       ALTER LINK foo {
!           SET TYPE default::Child2 USING (.foo[IS default::Child2]);
!       };
!   };
! };

$ rm dbschema/migrations/00002-*.edgeql
*

# Adding a comment to input doesn't affect result
$ printf "list\nyes\n.foo[IS default::Child2] # comment\n" | gel migration create --expert
! did you alter the type of link 'foo' of object type 'default::Base'? [y,n,l,c,b,s,q,?]
! The following DDL statements will be applied:
!     ALTER TYPE default::Base {
!         ALTER LINK foo {
!             SET TYPE default::Child2 USING (\(cast_expr));
!         };
!     };
! did you alter the type of link 'foo' of object type 'default::Base'? [y,n,l,c,b,s,q,?]
! Please specify a conversion expression to alter the type of link 'foo':
! cast_expr> 
! Created %{DATA}, id: %{DATA}

$ cat dbschema/migrations/00002-*.edgeql
! CREATE MIGRATION %{DATA}
!     ONTO m1d6kfhjnqmrw4lleqvx6fibf5hpmndpw2tn2f6o4wm6fjyf55dhcq
! {
!   ALTER TYPE default::Base {
!       ALTER LINK foo {
!           SET TYPE default::Child2 USING (.foo[IS default::Child2]);
!       };
!   };
! };

$ rm dbschema/migrations/00002-*.edgeql
*

# Explicit migration mode
$ printf "yes\n.foo[IS default::Child2] # comment\n" | gel migration create
! Running in interactive mode.
! HINT: Use `--expert` to run with less detailed prompts, or `--non-interactive`
!       to attempt to apply migrations without user input.
!
! did you alter the type of link 'foo' of object type 'default::Base'?
!
! If so, the following DDL statements will be applied:
!
!     ALTER TYPE default::Base {
!         ALTER LINK foo {
!             SET TYPE default::Child2 USING (\(cast_expr));
!         };
!     };
!
! Select an action:
!
! "yes" (or "y"): Confirm the prompt
! "no" (or "n"): Reject the prompt; server will attempt to generate another suggestion
! "confirmed" (or "c"): List already confirmed EdgeQL statements for the current migration
! "back" (or "b"): Go back a step by reverting latest accepted statements
! "stop" (or "s"): Stop and finalize migration with only current accepted changes
! "quit" (or "q"): Quit without saving changes
!
! Which action do you want to take? [y,n,c,b,s,q,?]
!
! Please specify a conversion expression to alter the type of link 'foo'.
! If left blank, the migration will use the default expression:
! .foo[IS default::Child2]
!
! Cast expression from "default::Child" to "default::Child2" (for ".foo"):
! cast_expr> 
! Created %{DATA}, id: %{DATA}
 
$ cat dbschema/migrations/00002-*.edgeql
! CREATE MIGRATION %{DATA}
!     ONTO m1d6kfhjnqmrw4lleqvx6fibf5hpmndpw2tn2f6o4wm6fjyf55dhcq
! {
!   ALTER TYPE default::Base {
!       ALTER LINK foo {
!           SET TYPE default::Child2 USING (.foo[IS default::Child2]);
!       };
!   };
! };
